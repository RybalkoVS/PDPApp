apply from: "$rootDir/buildscripts/app_properties.gradle"

ext {
    applyModuleConfig = { boolean isApp = false ->
        apply plugin: 'org.jetbrains.kotlin.android'

        if (isApp) {
            apply plugin: 'com.android.application'

            android {
                defaultConfig {
                    applicationId default_app_package
                }

                buildTypes {
                    applicationVariants.all { variant ->
                        variant.outputs.all {
                            outputFileName = "$default_app_package.${variant.buildType.name}.apk"
                        }
                    }
                }
                flavorDimensions "version"
                productFlavors {
                    beta {
                        applicationIdSuffix ".beta"
                    }
                    full {
                        applicationIdSuffix ".full"
                    }
                }
            }
        } else {
            apply plugin: 'com.android.library'
        }

        android {
            namespace = default_app_package
            compileSdk compile_sdk_version

            buildFeatures {
                compose true
            }

            composeOptions {
                kotlinCompilerExtensionVersion = compose_compiler_version
            }

            defaultConfig {
                minSdk min_sdk_version
                targetSdk target_sdk_version
                versionCode 1
                versionName "1.0"

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            buildTypes {
                release {
                    minifyEnabled true
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
                debug {
                    minifyEnabled false
                }
            }
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }
}